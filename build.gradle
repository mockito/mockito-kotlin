buildscript {
    repositories {
        mavenLocal()        // for local testing
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.shipkit:shipkit-changelog:1.1.13"
        classpath "pl.allegro.tech.build:axion-release-plugin:1.13.0"
        classpath "io.github.gradle-nexus:publish-plugin:1.0.0"
    }
}

apply plugin: "io.github.gradle-nexus.publish-plugin"
apply plugin: "org.shipkit.shipkit-changelog"
apply plugin: "org.shipkit.shipkit-github-release"
apply plugin: "pl.allegro.tech.build.axion-release"

scmVersion {
    tag {
        prefix = ''
    }
}

allprojects {
    group = 'org.mockito.kotlin'
    version = scmVersion.version
}

tasks.named("generateChangelog") {
    previousRevision = scmVersion.previousVersion
    githubToken = System.getenv("GITHUB_TOKEN")
    repository = "mockito/mockito-kotlin"
    releaseTag = project.version
}

tasks.named("githubRelease") {
    def genTask = tasks.named("generateChangelog").get()
    dependsOn genTask
    repository = genTask.repository
    changelog = genTask.outputFile
    githubToken = System.getenv("GITHUB_TOKEN")
    newTagRevision = System.getenv("GITHUB_SHA")
    releaseTag = project.version
    releaseName = project.version
}

nexusPublishing {
    repositories {
        if (System.getenv("NEXUS_TOKEN_PWD")) {
            sonatype {
                // Publishing to: https://s01.oss.sonatype.org (faster instance)
                nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
                snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")

                username = System.getenv("NEXUS_TOKEN_USER")
                password = System.getenv("NEXUS_TOKEN_PWD")
            }
        }
    }
}

if (version.endsWith("-SNAPSHOT")) {
    tasks.named("githubRelease") {
        //snapshot versions do not produce changelog / GitHub releases
        enabled = false
    }
    tasks.named("closeAndReleaseStagingRepository") {
        //snapshot binaries are available in Sonatype without the need to close the staging repo
        enabled = false
    }
}