import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

apply plugin: 'kotlin'
apply from: '../gradle/publishing.gradle'
apply plugin: 'org.jetbrains.dokka'

group = 'org.mockito.kotlin'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib"
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0'

    api "org.mockito:mockito-core:5.7.0"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.nhaarman:expect.kt:1.0.1'

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0'

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0"
}

dokkaHtml.configure {
    outputDirectory.set(file("$buildDir/javadoc"))

    dokkaSourceSets {
        named("main") {
            localDirectory.set(file("src/main/kotlin"))
            remoteUrl.set(new URL("https://github.com/nhaarman/mockito-kotlin/tree/master/mockito-kotlin/src/main/kotlin"))
            remoteLineSuffix.set("#L")
        }
    }
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_11)
        targetCompatibility = "11"
    }
}

javadoc.dependsOn dokkaHtml
