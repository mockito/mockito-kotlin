apply plugin: 'kotlin'

buildscript {
  ext.kotlin_version = '1.0.4'

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  compile "org.mockito:mockito-core:2.1.0"

  /* Tests */
  testCompile "junit:junit:4.12"
  testCompile "com.nhaarman:expect.kt:0.6.0"
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      artifact javadocJar
      artifact sourcesJar

      groupId 'com.nhaarman'
      artifactId 'mockito-kotlin'
      version rootProject.ext.versionName
    }
  }
}

bintray {
  user = hasProperty('bintray_user') ? bintray_user : System.getenv('BINTRAY_USER')
  key = hasProperty('bintray_key') ? bintray_key : System.getenv('BINTRAY_KEY')
  publications = ['MyPublication']

  pkg {
    repo = 'maven'
    name = "Mockito-Kotlin"
    desc = "Using Mockito with Kotlin"

    licenses = ['MIT']
    vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'

    version {
      name = rootProject.ext.versionName
      desc = 'Using Mockito with Kotlin'
      vcsTag = rootProject.ext.versionName
    }
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

// In order to keep the new Mockito configuration from messing with some
// valuable functional tests, we want to put the Inline-Mock-Maker plugin-based
// tests in their own source set.
sourceSets {
  testInlineMockito {
    kotlin//.srcDir 'src/testInlineMockito/kotlin'
    resources//.srcDir 'src/testInlineMockito/resources'

    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

configurations {
  testInlineMockitoCompile.extendsFrom testCompile
  testInlineMockitoRuntime.extendsFrom testRuntime
}

// define custom test task for running integration tests
task testInlineMockito(type: Test) {
  testClassesDir = sourceSets.testInlineMockito.output.classesDir
  classpath = sourceSets.testInlineMockito.runtimeClasspath
}

check.dependsOn testInlineMockito