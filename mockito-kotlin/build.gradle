apply plugin: 'kotlin'
apply from: './publishing.gradle'
apply plugin: 'org.jetbrains.dokka'

buildscript {
    ext.kotlin_version = '1.0.4'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.9"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    testInlineMockito {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    testInlineMockitoCompile.extendsFrom testCompile
    testInlineMockitoRuntime.extendsFrom testRuntime
}

// define custom test task for running integration tests
task testInlineMockito(type: Test) {
    testClassesDir = sourceSets.testInlineMockito.output.classesDir
    classpath = sourceSets.testInlineMockito.runtimeClasspath
}

test.dependsOn testInlineMockito


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.mockito:mockito-core:2.2.6"

    /* Tests */
    testCompile "junit:junit:4.12"
    testCompile "com.nhaarman:expect.kt:0.6.0"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/nhaarman/mockito-kotlin/tree/master/mockito-kotlin/src/main/kotlin"
        suffix = "#L"
    }
}
javadoc.dependsOn dokka